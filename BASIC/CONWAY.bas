10 CLEAR
20 RANDOMIZE
30 DIM A(39,39),B(39,39)
40 CLS
50 FOR X=2 TO 38
60 FOR Y=2 TO 38
70 A(X,Y)= INT ( RND (1)*2 )
80 NEXT Y
90 NEXT X
100 REM MAIN 
110 FOR X = 2 TO 38
120 FOR Y = 2 TO 38
130 N=A(X-1,Y-1)+A(X-1,Y)+A(X-1,Y+1)
140 N=N+A(X,Y-1)+A(X,Y+1)
150 N=N+A(X+1,Y-1)+A(X+1,Y)+A(X+1,Y+1)
160 B(X,Y)=(N=3)+(A(X,Y)*(N=2))
170 NEXT Y
180 NEXT X
190 FOR X=2 TO 38
200 FOR Y=2 TO 38
210 IF B(X,Y) <> 0 THEN A(X,Y) = 1 ELSE A(X,Y) = 0
220 IF A(X,Y) THEN PSET (X,Y) ELSE PRESET (X,Y)
230 NEXT Y
240 NEXT X
250 GOTO 110
